(* Copyright (C) Cloud Software Group, Inc.

   This program is free software; you can redistribute it and/or modify
   it under the terms of the GNU Lesser General Public License as published
   by the Free Software Foundation; version 2.1 only. with the special
   exception on linking described in file LICENSE.

   This program is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
   GNU Lesser General Public License for more details.
*)

(* Generator of Go bindings from the datamodel *)

open Datamodel_types
open CommonFunctions

let templates_dir = "templates"

let ( // ) = Filename.concat

let snake_to_camel (s : string) : string =
  Astring.String.cuts ~sep:"_" s
  |> List.map (fun s -> Astring.String.cuts ~sep:"-" s)
  |> List.concat
  |> List.map String.capitalize_ascii
  |> String.concat ""

let render_template template_file json =
  let templ =
    string_of_file (templates_dir // template_file) |> Mustache.of_string
  in
  Mustache.render templ json

let generate_file rendered destdir output_file =
  let out_chan = open_out (destdir // output_file) in
  Fun.protect
    (fun () -> output_string out_chan rendered)
    ~finally:(fun () -> close_out out_chan)

module Json = struct
  type enum = (string * string) list

  module StringMap = Map.Make (String)

  type enums = enum StringMap.t

  let choose_enum _key a _b = Some a

  let merge_maps m maps =
    List.fold_left (fun acc map -> StringMap.union choose_enum acc map) m maps

  let rec string_of_ty_with_enums ty : string * enums =
    match ty with
    | SecretString | String ->
        ("string", StringMap.empty)
    | Int ->
        ("int", StringMap.empty)
    | Float ->
        ("float64", StringMap.empty)
    | Bool ->
        ("bool", StringMap.empty)
    | DateTime ->
        ("time.Time", StringMap.empty)
    | Enum (name, kv) ->
        let name = snake_to_camel name in
        (name, StringMap.singleton name kv)
    | Set ty ->
        let s, e = string_of_ty_with_enums ty in
        ("[]" ^ s, e)
    | Map (ty1, ty2) ->
        let s1, e1 = string_of_ty_with_enums ty1 in
        let s2, e2 = string_of_ty_with_enums ty2 in
        let ty = "map[" ^ s1 ^ "]" ^ s2 in
        (ty, StringMap.union choose_enum e1 e2)
    | Ref r ->
        (snake_to_camel r ^ "Ref", StringMap.empty)
    | Record r ->
        (snake_to_camel r ^ "Record", StringMap.empty)
    | Option ty ->
        string_of_ty_with_enums ty

  let of_enum name vs =
    let name = snake_to_camel name in
    let of_value (v, d) =
      `O
        [
          ("value", `String v)
        ; ("doc", `String d)
        ; ("name", `String (name ^ snake_to_camel v))
        ; ("type", `String name)
        ]
    in
    `O [("name", `String name); ("values", `A (List.map of_value vs))]

  let fields_of_obj obj =
    let rec flatten_contents contents =
      List.fold_left
        (fun l -> function
          | Field f ->
              f :: l
          | Namespace (_name, contents) ->
              flatten_contents contents @ l
        )
        [] contents
    in
    flatten_contents obj.contents

  let of_field field =
    let concat_and_convert field =
      let concated =
        String.concat "" (List.map snake_to_camel field.full_name)
      in
      match concated with
      | "Uuid" | "Id" ->
          String.uppercase_ascii concated
      | _ ->
          concated
    in
    let ty, _e = string_of_ty_with_enums field.ty in
    `O
      [
        ("name", `String (concat_and_convert field))
      ; ("description", `String (String.trim field.field_description))
      ; ("type", `String ty)
      ]

  let type_of_result msg =
    match msg.msg_result with None -> None | Some (t, _d) -> Some t

  let types_of_params obj =
    List.map
      (fun msg -> List.map (fun p -> p.param_type) msg.msg_params)
      obj.messages
    |> List.concat

  let types_of_results obj =
    List.filter_map (fun msg -> type_of_result msg) obj.messages

  let types_of_fields obj = fields_of_obj obj |> List.map (fun field -> field.ty)

  let modules_of_type = function
    | DateTime ->
        [`O [("name", `String "time"); ("sname", `Null)]]
    | _ ->
        []

  let modules_of_types types =
    let common = [`O [("name", `String "fmt"); ("sname", `Null)]] in
    let items =
      List.map modules_of_type types |> List.concat |> List.append common
    in
    `O [("import", `Bool true); ("items", `A items)]

  let all_enums objs =
    let enums =
      List.map
        (fun obj ->
          types_of_params obj @ types_of_fields obj @ types_of_results obj
          |> List.map (fun ty ->
                 let _, e = string_of_ty_with_enums ty in
                 e
             )
        )
        objs
      |> List.concat
      |> merge_maps StringMap.empty
    in
    `O
      [
        ( "enums"
        , `A (StringMap.fold (fun k v acc -> of_enum k v :: acc) enums [])
        )
      ]

  let xenapi objs =
    List.map
      (fun obj ->
        let fields = fields_of_obj obj in
        let types = List.map (fun field -> field.ty) fields in
        let modules =
          match obj.messages with [] -> `Null | _ -> modules_of_types types
        in
        let base_assoc_list =
          [
            ("name", `String (snake_to_camel obj.name))
          ; ("description", `String (String.trim obj.description))
          ; ( "fields"
            , `A (get_event_snapshot obj.name @ List.map of_field fields)
            )
          ; ("modules", modules)
          ]
        in
        let assoc_list = get_event_session_value obj.name @ base_assoc_list in
        (String.lowercase_ascii obj.name, `O assoc_list)
      )
      objs

  let api_messages =
    List.map (fun (msg, _) -> `O [("name", `String msg)]) !Api_messages.msgList

  let api_errors =
    List.map (fun error -> `O [("name", `String error)]) !Api_errors.errors
end
